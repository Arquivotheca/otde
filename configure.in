# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.

AC_INIT(acinclude.m4) dnl a source file from your sub dir
AC_MSG_WARN("This Code is alpha. It may not even compile.  Use at your own risk.")
AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM
AC_ISC_POSIX

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdebase, 0.10) dnl searches for some needed programs

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl checks, if the debug code is wanted
AC_CHECK_FLAGS

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
test "$CCC" = "" && CCC="xlc DCC"
AC_PROG_CXX
AC_PROG_LN_S
AC_PROG_RANLIB dnl for libkfm

case $host in 
*-*-linux* )
   AC_CHECK_HEADER(elf.h, 
     [ 
     	AC_DEFINE(HAVE_SHADOW) 
	ac_have_shadow="yes"
     ])
esac

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
AM_KDE_WITH_NLS

dnl I need to check for libsocket before testing X
AC_CHECK_LIB(socket, socket, [LIBSOCKET="-lsocket -lnsl"]) dnl for Solaris
AC_SUBST(LIBSOCKET)
AC_PATH_KDE

dnl this is for kdm:
dnl checks for X server
AC_PATH_PROG(X_SERVER, X)
if test ! -z "$X_SERVER"; then
X_SERVER=`echo $X_SERVER | sed -e 's+/X++'`
AC_DEFINE_UNQUOTED(XBINDIR,$X_SERVER)
XBINDIR=$XDM
AC_SUBST(XBINDIR)
fi

dnl This one tries to find XDMDIR for config files
AC_ARG_WITH(xdmdir,
	[  --with-xdmdir	          If the xdm config dir can't be found automaticly],
	[ ac_xdmdir=$withval],
	[ ac_xdmdir="no"])

AC_MSG_CHECKING([for xdm configuration dir])
if test "$ac_xdmdir" = "no"; then
    rm -fr conftestdir
    if mkdir conftestdir; then
	cd conftestdir
    cat > Imakefile <<'EOF'
acfindxdm:
	@echo 'ac_xdmdir="$(XDMDIR)";'
EOF
	if (xmkmf) > /dev/null 2> /dev/null && test -f Makefile; then
	    eval `${MAKE-make} acfindxdm 2>/dev/null 2>/dev/null | grep -v make`
	fi
	cd ..
	rm -fr conftestdir
	dnl Check if Imake was right
	if test -f $ac_xdmdir/xdm-config; then
	    AC_MSG_RESULT($ac_xdmdir)
	else
	    dnl Here we must do something else
	    dnl Maybe look for xdm-config in standard places, and
	    dnl if that fails use a fresh copy in $KDEDIR/config/kdm/
	    AC_FIND_FILE(xdm-config,/etc/X11/xdm /var/X11/xdm /usr/openwin/xdm /usr/X11R6/lib/X11/xdm,ac_xdmdir)
	    if test -f $ac_xdmdir/xdm-config; then
                AC_MSG_RESULT($ac_xdmdir)
            else                                 
		if test "${prefix}" = NONE; then
			ac_xdmdir=$ac_default_prefix/config/kdm
		else
			ac_xdmdir=$prefix/config/kdm
		fi
		AC_MSG_RESULT([xdm config dir not found, installing defaults in $ac_xdmdir])
		xdmconfigsubdir=xdmconfig
		AC_SUBST(xdmconfigsubdir)
	    fi
	fi
    fi
else
    if test -f $ac_xdmdir/xdm-config; then
	AC_MSG_RESULT($ac_xdmdir)
    else

	AC_MSG_RESULT([xdm config dir not found, installing defaults in $ac_xdmdir])
	xdmconfigsubdir=xdmconfig
	AC_SUBST(xdmconfigsubdir)
    fi
fi
AC_DEFINE_UNQUOTED(XDMDIR,"$ac_xdmdir")
AC_SUBST(ac_xdmdir)

AC_PATH_PAM
if test "x$no_pam" = "xyes"; then 
	pam_support="yes";
else
	pam_support="no";
fi

AC_ARG_WITH(shadow,
	[  --with-shadow		  If you want shadow password support ],
	[ shadow_support="yes"
	  if test "$pam_support" = "yes"; then
		AC_MSG_WARN("You can not define both pam AND shadow")
	  fi
	],
	[ shadow_support="no" ] )

if test "$pam_support" = "yes"; then
  AC_CHECK_LIB(pam, main, [PASSWDLIB="-lpam -ldl"
  AC_DEFINE_UNQUOTED(HAVE_PAM_LIB)],
  [],-ldl)
fi

if test -z "$PASSWDLIB" && test "$shadow_support" = "yes"; then
  AC_CHECK_LIB(shadow, main,
    [ PASSWDLIB="-lshadow"
      AC_DEFINE_UNQUOTED(HAVE_SHADOW_LIB)
    ])
fi
AC_SUBST(PASSWDLIB)



AC_ARG_ENABLE(setuid,[  --enable-setuid         enable setuid for kscreensavers [default=no]],
[ if test $enableval = "no"; 
    then ac_enable_setuid="no"
    else ac_enable_setuid="yes"
  fi
],
ac_enable_setuid="no"
)

if test "$SETUIDFLAGS" = ""; then 
 if test "$ac_enable_setuid" = "no"; then
  if test "$ac_have_shadow" = "yes"; then
    AC_WARN("You have shadow password! You should use --enable-setuid!")
  fi
  SETUIDFLAGS=""
 else
  SETUIDFLAGS="-m 4755"
 fi
else
 SETUIDFLAGS="-m $SETUIDFLAGS"
fi
AC_SUBST(SETUIDFLAGS)

AC_LANG_CPLUSPLUS
dnl Checks for libraries. 
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD
AC_SUBST(LIBCOMPAT)
AC_CHECK_LIB(crypt, main, [LIBCRYPT="-lcrypt"]) dnl for BSD
AC_SUBST(LIBCRYPT)
LIBKHTMLW="-lkhtmlw -ljscript" dnl this is somewhat historical, since jscript is ways needed
AC_SUBST(LIBKHTMLW)

AC_FIND_GIF
AC_FIND_JPEG
AC_HAVE_GL(GL_SAVERS="kmorph3d.kss",GL_SAVERS="")
AC_SUBST(GL_SAVERS)
AC_HAVE_XPM(XPM_SAVERS="kbat.kss",XPM_SAVERS="")
AC_SUBST(XPM_SAVERS)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h crypt.h sys/select.h \
	sys/ioctl.h sys/stropts.h termio.h termios.h lastlog.h malloc.h \
	sys/sockio.h crypt.h krb5/krb5.h rpc/rpc.h rpc/key_prot.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
AC_C_LONG_DOUBLE
AC_TYPE_GETGROUPS

dnl Checks for library functions. MISCOBJS is for kdecore
AC_CHECK_FUNCS(socket powl sqrtl strdup getdtablesize vsnprintf setpgid)
AC_CHECK_KSIZE_T
AC_CHECK_SETENV

dnl output files
topdir=`pwd`
AC_SUBST(topdir)

dnl Perform program name transformation
AC_ARG_PROGRAM

AC_OUTPUT(Makefile \
	kpanel/Makefile \
	kpanel/pics/Makefile \
	kdehelp/Makefile \
	kdehelp/index/Makefile \
	kdehelp/search/Makefile \
	kfm/Makefile \
	kfm/Desktop/Makefile \
	kfm/Desktop/Templates/Makefile \
	kfm/client/Makefile \
	kfm/kioslave/Makefile \
	kfm/pics/Makefile \
	kfm/kfmwarn/Makefile \
	kfm/kfmexec/Makefile \
	mimetypes/Makefile \
	mimetypes/application/Makefile \
	mimetypes/text/Makefile \
	mimetypes/video/Makefile \
	mimetypes/image/Makefile \
	mimetypes/text/Makefile \
	mimetypes/audio/Makefile \
	mimetypes/inode/Makefile \
	kdisplay/Makefile \
	kfind/Makefile \
	kfind/pics/Makefile \
	kfontmanager/Makefile \
	klogout/Makefile \
	kscreensaver/Makefile \
	kvt/Makefile \
	kwm/Makefile \
	kwm/pics/Makefile \
	kwmclient/Makefile \
	pics/Makefile \
	pics/mini/Makefile \
	pics/toolbar/Makefile \
	pics/wallpapers/Makefile \
	kdm/Makefile \
	kdm/config/Makefile \
	kdm/pics/Makefile \
	kdm/xdmconfig/Makefile \
	kaudio/Makefile \
	kmenuedit/Makefile \
	kmenuedit/icons/Makefile \
	doc/Makefile \
	doc/kfm/Makefile \
	doc/kfind/Makefile \
	doc/kdehelp/Makefile \
	doc/kfontmanager/Makefile \
	doc/kdm/Makefile \
	doc/kdisplay/Makefile \
	doc/kwm/Makefile \
	doc/kvt/Makefile \
	doc/kmenuedit/Makefile \
	applnk/Applications/Makefile \
	applnk/Games/Makefile \
	applnk/Graphics/Makefile \
	applnk/Internet/Makefile \
	applnk/Multimedia/Makefile \
	applnk/System/Core/Makefile \
	applnk/System/Makefile \
	applnk/Utilities/Makefile \
	applnk/Makefile \
	po/Makefile \
	po/cs/Makefile \
	po/da/Makefile \
	po/de/Makefile \
	po/es/Makefile \
	po/fi/Makefile \
	po/fr/Makefile \
	po/it/Makefile \
	po/nl/Makefile \
	po/ru/Makefile \
	po/sv/Makefile 
)


