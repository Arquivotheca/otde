# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.
AC_INIT(acinclude.m4) dnl a source file from your sub dir
AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdelibs, 0.9) dnl searches for some needed programs

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl checks, if the debug code is wanted
AC_CHECK_FLAGS

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
test "$CCC" = "" && CCC="xlc DCC"
AC_PROG_CXX
AC_PROG_LN_S
dnl libtool is only for C, so I must force him
dnl to find the correct flags for C++
ac_save_cc=$CC
CC=$CXX
AM_PROG_LIBTOOL dnl for libraries
CC=$ac_save_cc

dnl find out some variables (not supported yet)
AC_AIX
AC_MINIX

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files, GNU_GETTEXT for the sources
AM_KDE_WITH_NLS
AM_KDE_GNU_GETTEXT  

dnl Checks for header files.
AC_LANG_CPLUSPLUS
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/cdefs.h)

dnl Checks for libraries.
AC_CHECK_LIB(socket, socket, [LIBSOCKET="-lsocket -lnsl"]) dnl for Solaris
AC_PATH_KDE
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD
AC_FIND_GIF
AC_FIND_JPEG

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL

dnl Checks for library functions. 
AC_CHECK_FUNCS(socket)
AC_CHECK_SETENV
AC_LANG_CPLUSPLUS
AC_CHECK_KSIZE_T

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl output files
topdir=`pwd`
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(topdir)
AC_SUBST(LIBSOCKET)
AC_SUBST(LIBCOMPAT)
if test "${prefix}" = NONE; then
  KDEDIR=$ac_default_prefix
 else
  KDEDIR=$prefix
fi
AC_SUBST(KDEDIR)

AC_OUTPUT(Makefile kdecore/Makefile kdeui/Makefile khtmlw/Makefile kdetest/Makefile kdedoc/Makefile jscript/Makefile kfmlib/Makefile mediatool/Makefile po/Makefile)
